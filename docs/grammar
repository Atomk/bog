root : (stmt NL)* EOF

stmt
    : decl "=" block_or_expr
    | "type" IDENTIFIER "=" type_expr
    | expr

decl : ("let" | "const") primary_expr (":" type_expr)?

type_expr
    : type_primitive
    | IDENTIFIER (":" type_expr)? ("|" IDENTIFIER (":" type_expr)?)

type_primitive
    : "any"
    | "int"
    | "num"
    | "str"
    | "none"
    | "type"
    | "bool"
    | IDENTIFIER
    | "[" type_expr "]"
    | "(" type_expr ")"
    | "(" (type_expr ",")+ type_expr? ")"
    | "{" (expr ":" type_expr ",")* (expr ":" type_expr)? "}"
    | "fn" "(" (type_expr ",")* type_expr? ")" ":" type_expr
    | "error" "(" type_expr ")"

expr
    : fn
    | jump_expr
    | bool_expr assign?

block_or_expr : block | expr

block : NL BEGIN (stmt NL)+ END

assign : ("=" | "+=" | "-=" | "*=" | "**=" | "/=" | "//=" | "%=" | "<<=" | ">>=" | "&=" | "|=" | "^=") block_or_expr

jump_expr : "return" block_or_expr? | "break" | "continue"

fn : "fn" "(" (primary_expr (":" type_expr)? ",")* (primary_expr (":" type_expr)?)? ")" (":" type_expr)? block_or_expr

bool_expr
    : "not" comparision_expr
    | comparision_expr ("or" comparision_expr)*
    | comparision_expr ("and" comparision_expr)*

comparision_expr
    : range_expr (("<" | "<=" | ">" | ">="| "==" | "!=" | "in") range_expr)?
    | range_expr ("is" type_expr)?

range_expr : bit_expr ("..." bit_expr)?

bit_expr : shift_expr (("&" shift_expr)* | ("|" shift_expr)* | ("^" shift_expr)* | ("catch" ("(" decl ")")? block_or_expr))?

shift_expr : add_expr (("<<" | ">>") add_expr)?

add_expr : mul_expr (("-" | "+") mul_expr)*

mul_expr : cast_expr (("*" | "/" | "//" | "%") cast_expr)*

cast_expr : prefix_expr ("as" type_expr)?

prefix_expr : ("try" | "-" | "+" | "~")? power_expr

power_expr : primary_expr suffix_expr* ("**" power_expr)?

suffix_expr
    : "[" expr "]"
    | "(" (expr ",")* expr? ")"
    | "." IDENTIFIER

primary_expr
    : IDENTIFIER
    | "_"
    | STRING
    | NUMBER
    | "true"
    | "false"
    | "this"
    | "." IDENTIFIER ("=" expr)?
    | "(" block_or_expr ")"
    | "(" (expr ",")+ expr? ")"
    | "{" (expr ":" expr ",")* (expr ":" expr)? "}"
    | "[" (expr ",")* expr? "]"
    | "error" "(" expr ")"
    | "import" "(" STRING ")"
    | "native" "(" STRING ("," type_expr)? ")" 
    | if
    | while
    | for
    | match

if : "if" "(" (decl "=")? expr ")" block_or_expr ("else" block_or_expr)?

while : "while" "(" (decl "=")? expr ")" block_or_expr

for : "for" "(" (decl "in")? expr ")" block_or_expr

match : "match" "(" expr ")" (NL match_case)+ NL

match_case
    : decl "=>" block_or_expr
    | expr ("," expr)* ","? "=>" block_or_expr
