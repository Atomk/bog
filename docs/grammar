root : (stmt NL)* EOF

stmt
    : decl "=" expr
    | expr

decl : ("let" | "const") primary_expr

expr
    : fn
    | jump_expr
    | bool_expr assign?
    | block

block : NL BEGIN (stmt NL)+ END

assign : ("=" | "+=" | "-=" | "*=" | "**=" | "/=" | "//=" | "%=" | "<<=" | ">>=" | "&=" | "|=" | "^=") expr

jump_expr : "return" expr? | "break" | "continue"

fn : "fn" "(" (primary_expr ",")* primary_expr? ")" expr

bool_expr
    : "not" comparision_expr
    | comparision_expr ("or" comparision_expr)*
    | comparision_expr ("and" comparision_expr)*

comparision_expr
    : range_expr (("<" | "<=" | ">" | ">="| "==" | "!=" | "in") range_expr)?
    | range_expr ("is" type_name)?

type_name : "none" | "int" | "num" | "bool" | "str" | "tuple" | "map" | "list" | "error" | "range" | "fn"

range_expr : bit_expr ("..." bit_expr)?

bit_expr : shift_expr (("&" shift_expr)* | ("|" shift_expr)* | ("^" shift_expr)* | ("catch" ("(" decl ")")? expr))?

shift_expr : add_expr (("<<" | ">>") add_expr)?

add_expr : mul_expr (("-" | "+") mul_expr)*

mul_expr : cast_expr (("*" | "/" | "//" | "%") cast_expr)*

cast_expr : prefix_expr ("as" type_name)?

prefix_expr : ("try" | "-" | "+" | "~")? power_expr

power_expr : primary_expr suffix_expr* ("**" power_expr)?

suffix_expr
    : "[" expr "]"
    | "(" (expr ",")* expr? ")"
    | "." IDENTIFIER

primary_expr
    : IDENTIFIER
    | "_"
    | STRING
    | NUMBER
    | "true"
    | "false"
    | "(" expr ")"
    | "(" (expr ",")+ expr? ")"
    | "{" (expr ":" expr ",")* (expr ":" expr)? "}"
    | "[" (expr ",")* expr? "]"
    | "error" "(" expr ")"
    | "import" "(" STRING ")"
    | "native" "(" STRING ")"
    | if
    | while
    | for
    | match

if : "if" "(" (decl "=")? expr ")" expr ("else" expr)?

while : "while" "(" (decl "=")? expr ")" expr

for : "for" "(" (decl "in")? expr ")" expr

match : "match" "(" expr ")" (NL match_case)+ NL

match_case
    : decl ":" expr
    | expr ("," expr)* ","? ":" expr
