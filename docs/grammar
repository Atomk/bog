root : (stmt NL)* EOF

stmt : let | expr

let : "let" unwrap "=" expr

unwrap
    : IDENTIFIER
    | "{" ((expr ":")? (unwrap | "_") ",")* ((expr ":")? (unwrap | "_"))?"}"
    | "(" ((unwrap | "_") ",")* (unwrap | "_")? ")"
    | "[" ((unwrap | "_") ",")* (unwrap | "_")? "]"
    | "error" "(" (unwrap | "_") ")"

expr
    : fn 
    | jump_expr
    | bool_expr

jump_expr : "return" expr? | "break" expr? | "continue"

fn : "fn" "(" (unwrap ",")* unwrap? ")" expr

bool_expr
    : "not" comparision_expr
    | comparision_expr ("or" comparision_expr)* 
    | comparision_expr ("and" comparision_expr)*

comparision_expr 
    : range_expr (("<" | "<=" | ">" | ">="| "==" | "!=" | "in") range_expr)?
    | range_expr ("is" type_name)?

type_name : "none" | "int" | "num" | "bool" | "str" | "tuple" | "map" | "list" | "error" | "range" | "fn"

range_expr : bit_expr ("..." bit_expr)?

bit_expr : shift_expr (("&" shift_expr)* | ("|" shift_expr)* | ("^" shift_expr)*) | ("catch" ("let" unwrap ":")? expr)

shift_expr : add_expr (("<<" | ">>") add_expr)

add_expr : mul_expr (("-" | "+") mul_expr)*

mul_expr : cast_expr (("*" | "/" | "//" | "%") cast_expr)*

cast_expr : prefix_expr ("as" type_name)?

prefix_expr
    : ("try" | "-" | "+" | "~") power_expr
    | power_expr

power_expr : primary_expr suffix_expr*  (assign? | ("**" power_expr)?)

assign
    : "=" expr
    | ("+=" | "-=" | "*=" | "**=" | "/=" | "//=" | "%=" | "<<=" | ">>=" | "&=" | "|=" | "^=") bit_expr

suffix_expr
    : "[" expr "]"
    | "(" (expr ",")* expr? ")"
    | "." IDENTIFIER

primary_expr
    : IDENTIFIER
    | STRING
    | NUMBER
    | "true" 
    | "false"
    | "(" expr ")"
    | "(" (expr ",")+ expr? ")"
    | "{" (expr ":" expr ",")* (expr ":" expr)? "}"
    | "[" (expr ",")* expr? "]"
    | "error" "(" expr ")"
    | "import" "(" STRING ")"
    | block
    | if
    | while
    | for
    | match

block : "{" (NL stmt)+ "}"

if : "if" "(" ("let" unwrap "=") bool_expr ")" expr ("else" expr)?

while : "while" "(" ("let" unwrap "=") bool_expr ")" expr

for : "for" "(" ("let" unwrap "in") range_expr ")" expr

match : "match" "(" bool_expr ")" "{" (NL match_case)+ NL "}"

match_case
    : "let" unwrap ":" expr
    | "_" ":" expr
    | expr ("," expr)* ","? ":" expr
